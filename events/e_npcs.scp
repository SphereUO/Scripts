//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1

[DEFNAME extendedAI]
ExtendedAI=1


[FUNCTION f_addExtendedAIEvent]
if (<def.ExtendedAI>==1)
    events +<args>
endif


[EVENTS e_Undead]
ON=@EnvironChange
   Return 0
   // Enter "//" before Return 0, if you want to make script working
   // i like the dark
   if (<sector.isdark>) || (<flags>& (statf_nightsight|statf_indoors))
      if (!<tag0.lightstr>)
         return 0
      endif
      anim 011
      bark 4
      str=<tag.lightstr>   // restore me.
      hits=<str>
      tag.lightstr=
      karma=<tag0.lightkarma>
      tag.lightkarma=//just dispose of this.
      fame=<tag0.lightfame>
      tag.lightfame=
      flags ^= statf_conjured   // no loot if killed.
      return 0
   endif
   // i can't live in light areas. weaken me ? or destroy me ?
   anim 014
   bark 4
   if (<tag0.lightstr>)
      return 0
   endif
   if ( <flags>&statf_conjured   )
      remove
      return 1
   endif
   flags |= statf_conjured   // no loot if killed.
   tag.lightstr=<str>
   str=1
   hits=1   // very weak.
   tag.lightkarma=<karma>   // killing it means nothing now.
   karma=-2
   tag.lightfame=<fame>
   fame=10
   return 0

[EVENTS e_polymorphed_lich]
ON=@SPELLEFFECT
// Dispell spells will return a polymorphed lich into lich.
IF ((<SERV.SPELL.<ARGN1>.DEFNAME> == s_dispel) || (<SERV.SPELL.<ARGN1>.DEFNAME> == s_mass_dispel))
   TRYSRC <I.UID> SRC.EFFECT 2,i_fx_smoke_small,16,10,0
   I.BODY = c_lich
   I.EVENTS -e_polymorphed_lich
   TIMERF <I.TAG.BEHAVIOURDELAY>,I.f_lich_polymorph
ENDIF
RETURN 0

[EVENTS e_lich_death]
// Check for summoned skeletons to remove him.
ON=@DEATH
FOR 0 <EVAL <TAGCOUNT> - 1>
    // Loop hover tags. If the value is the UID of a c_skeleton, dispel it.
    LOCAL.UID = <TAGAT.<LOCAL._FOR>.VALUE>
    IF (<UID.<LOCAL.UID>>)
        IF (<UID.<LOCAL.UID>.DEFNAME> == c_skeleton)
            UID.<LOCAL.UID>.HITS = 0
        ENDIF
    ENDIF
ENDFOR
RETURN 0

[EVENTS e_skeleton_summoned_by_lich]
ON=@SPELLEFFECT
// Dispell spells will remove summoned skeletons.
IF ((<SERV.SPELL.<ARGN1>.DEFNAME> == s_dispel) || (<SERV.SPELL.<ARGN1>.DEFNAME> == s_mass_dispel))
   I.HITS = 0
ENDIF
RETURN 0

ON=@DEATH
REF1 = <TAG.MASTER>
// First check if c_lich exists.
IF (!<REF1>)
    RETURN 0
ENDIF
// We must check if the f_lich_summon_skeletons needs to be invoked.
LOCAL.LIMIT = <REF1.CURFOLLOWER> + <I.FOLLOWERSLOTS>
IF (<LOCAL.LIMIT> > <REF1.MAXFOLLOWER>)
    REF1.TIMERF <REF1.TAG.BEHAVIOURDELAY>,f_lich_summon_skeletons
ENDIF
REF1.TAG.SLOT_<I.UID> =
REF1.CURFOLLOWER = <EVAL <REF1.CURFOLLOWER> - <I.FOLLOWERSLOTS>>
RETURN 0


[EVENTS e_extendedAI_aura_frost]
ON=@GetHit
//Ice Aura Attack
IF !(<R3>)
  REF11=<UID>
  FORCHARS 3
     IF (<ISPLAYER>) || (<FLAGS>&statf_pet)
        LOCAL.DAM=<R10,20>
        DAMAGE <LOCAL.DAM> <DEF.dam_cold> <REF11>
        SYSMESSAGE Intense cold radiating from the demon damages you
     ENDIF
  END
ENDIF


[EVENTS e_extendedAI_aura_fire]
ON=@GetHit
//Fire Aura Attack
IF ( (<MANA> >= 20) && !(<R5>) )
  MANA -= 20
  REF11=<UID>
  FORCHARS 3
     IF (<ISPLAYER>) || (<FLAGS>&statf_pet)
        LOCAL.DAM=<R10,20>
        DAMAGE <LOCAL.DAM> <DEF.dam_fire> <REF11>
        SYSMESSAGE Intense heat radiating from the serpent damages you
     ENDIF
  END
ENDIF


[EVENTS e_extendedAI_aura_summonDreadSpider]
ON=@GetHit
IF !(<R9>)
    SERV.NEWNPC c_spider_dread
    NEW.P=<P>
    NEW.HOME=<NEW.P>
    NEW.HOMEDIST=7
    NEW.REMOVETIMER 120
endif


[EVENTS e_extendedAI_hit_etherealWarrior]
ON=@Hit
HITS += <EVAL <ARGN1>/6>
IF (<HITS> > <MAXHITS>)
    HITS=<MAXHITS>
endif
STAM += <EVAL <ARGN1>/12>
IF (<STAM> > <MAXSTAM>)
    STAM=<MAXSTAM>
endif
SRC.STAM -= <EVAL <ARGN1>/12>
IF (<SRC.STAM> < 0)
    SRC.STAM=0
endif
MANA += <EVAL <ARGN1>/9>
IF (<MANA> > <MAXMANA>)
    MANA=<MAXMANA>
endif
SRC.MANA -= <EVAL <ARGN1>/9>
IF (<SRC.MANA> < 0)
    SRC.MANA=0
endif


[EVENTS e_extendedAI_plageBeast_eatCorpse]
ON=@ItemStep
IF (<ACT.TYPE>==t_corpse)
    IF !(<ACT.TAG0.PLAGUEEATEN>)
        MAXHITS += 200
        HITS += 200
        ACT.TAG.PLAGUEEATEN=1
        SAY @03c1,3,1 * <NAME> eats a corpse *
    endif
endif


[EVENTS e_extendedAI_aura_summon_fireAntWorker]
ON=@GetHit
IF !(<R10>)
    SERV.NEWNPC C_FIRE_ANT_WORKER
    NEW.P=<P>
    NEW.HOME=<NEW.P>
    NEW.HOMEDIST=7
    NEW.REMOVETIMER 120
endif

[EOF]
