//***********************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereServer.net
//***********************************************************************************
// Save in ANSI encoding
//***********************************************************************************
// New Multilanguage Admin
// Created by Golfin (http://eranova.cz/) and Jhobean (https://www.uocryptonite.com/)
//***********************************************************************************
VERSION=X1

[DIALOG d_SphereAdmin_PlayerTags]
50,50
Src.Dialogclose d_SphereAdmin_PlayerTags

Page 0
Resizepic 0 0 5054 652 507
Gumppictiled 5 5 640 497 2604
Gumppictiled 5 5 640 25 2624
Gumppictiled 5 35 145 285 2624
Gumppictiled 5 325 145 177 2624
Gumppictiled 155 35 490 467 2624
Checkertrans 5 5 640 497

DText 10 7 0c1 SphereServer <Src.Version> Admin Panel (<Serv.Servname>) - <Def.d_admin_<Src.CTag0.AccountLang>_Tags_for_Character> <Name>
Button 615 5 4017 4018 1 0 0
Button 585 5 4008 4009 1 0 1

DText 10 37 0c1 <Def.d_admin_<Src.CTag0.AccountLang>_Account>

DText 40 62 0480 <Def.d_admin_<Src.CTag0.AccountLang>_Info_Account>
Button 5 60 4005 4006 1 0 2

DText 40 87 0480 <Def.d_admin_<Src.CTag0.AccountLang>_Action_Acc>
Button 5 85 4005 4006 1 0 3

DText 40 112 0480 <Def.d_admin_<Src.CTag0.AccountLang>_Notes>
Button 5 110 4005 4006 1 0 4

DText 10 142 0c1 <Def.d_admin_<Src.CTag0.AccountLang>_Character>

DText 40 167 0480 <Def.d_admin_<Src.CTag0.AccountLang>_Properties>
Button 5 165 4005 4006 1 0 5

DText 40 192 0480 <Def.d_admin_<Src.CTag0.AccountLang>_Skills>
Button 5 190 4005 4006 1 0 6

DText 40 217 0480 <Def.d_admin_<Src.CTag0.AccountLang>_Flags>
Button 5 215 4005 4006 1 0 7

DText 40 242 0480 <Def.d_admin_<Src.CTag0.AccountLang>_Tags>
Button 5 240 4005 4006 1 0 8

DText 40 267 0480 <Def.d_admin_<Src.CTag0.AccountLang>_Events>
Button 5 265 4005 4006 1 0 9

Page 1
DText 160 35 0c1 <Def.d_admin_<Src.CTag0.AccountLang>_Del_Tags>
If (0<TagCount>)
   Local.Page=1
   Local.oy=60
   Page 1
   For x 0 <Eval <TagCount>-1>
      If (<Local.oy> > 450)
         Local.oy = 60
         Local.Page += 1
         Button 615 480 4005 4006 0 <Eval <Local.Page>> 0 // Forward Button
         Page <Eval <Local.Page>>
         Button 150 480 4014 4015 0 <Eval <Local.Page> - 1> 0 // Back Button
      Endif
      DText 195 <Local.oy> 0480 Tag.<Tagat.<Local.x>.key> = <Tagat.<Local.x>.val>
      If (<Src.Account.Plevel> > 3)
         Button 160 <Local.oy> 4017 4018 1 0 <Eval (1000 + <Local.x>)>
      Endif
      Local.oy += 25
   end
Else
   DText 160 60 0c1 <Def.d_admin_<Src.CTag0.AccountLang>_No_Tags>
Endif

[DIALOG d_SphereAdmin_PlayerTags Button]
On=0 1
   Src.Dialog d_SphereAdmin_PlayerTweak

On=2
   Dialog d_SphereAdmin_PlayerTweak 1

On=3
   Dialog d_SphereAdmin_PlayerTweak 2

On=4
   Dialog d_SphereAdmin_PlayerNotes

On=5
   Dialog d_SphereAdmin_PlayerTweak 3

On=6
   Dialog d_SphereAdmin_PlayerTweak 4

On=7
   Dialog d_SphereAdmin_PlayerTweak 5

On=8
   Dialog d_SphereAdmin_PlayerTags

On=9
   Dialog d_SphereAdmin_PlayerTweak 6

On=1000,1999
	Tag.<Tagat.<Eval <ArgN>-1000>.Key>=
	Dialog d_SphereAdmin_PlayerTags
	Src.Sysmessage @<def.tcolor_orange> <Def.d_admin_<Src.CTag0.AccountLang>_Tags_Sys> <Name> <Def.d_admin_<Src.CTag0.AccountLang>_Updated_Sys>

[EOF]
