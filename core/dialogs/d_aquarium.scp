//****************************************************************************
// SphereServer by: SphereServer development team and Menasoft.
// www.sphereserver.net
//****************************************************************************
VERSION=X1

[COMMENT]
This aquarium system still incomplete yet.
Only items and basic features (add/remove items on aquarium) are done.
Others features such as aquarium state, events and rewards will be added later.

[DIALOG d_aquarium]
100,100

page 0
resizepic 0 0 0e10 350 323
gumppic 0 0 <QVAL (<DISPIDDEC> >= 41894)? 09d36 : 02c96>

IF (<RESCOUNT>)
   FOR 1 <RESCOUNT>
      page <LOCAL._FOR>
      REF1=<FINDCONT.<eval <LOCAL._FOR>-1>>

      tilepichue 150 80 <REF1.DISPIDDEC> <dREF1.COLOR>
      dhtmlgump 20 195 250 20 0 0 <DEF.BFONT_WHITE><dLOCAL._FOR>/<RESCOUNT>
      dhtmlgump 20 217 250 20 0 0 <DEF.BFONT_WHITE><REF1.NAME>
      dhtmlgump 20 239 315 20 0 0 <DEF.BFONT_WHITE><QVAL (<REF1.TAG0.AquariumCreature>)? A live aquarium creature : An aquarium decoration>

      resizepic 230 195 013be 100 26
      button 235 200 0845 0846 1 0 <LOCAL._FOR>
      dhtmlgump 260 198 60 26 0 0 Remove

      IF (<FINDCONT.<eval <LOCAL._FOR>-2>>)
         button 45 280 0fae 0faf 0 <eval <LOCAL._FOR>-1> 0
         dhtmlgump 80 283 100 18 0 0 <DEF.BFONT_WHITE>PREV PAGE
      ENDIF
      IF (<FINDCONT.<LOCAL._FOR>>)
         button 195 280 0fa5 0fa7 0 <eval <LOCAL._FOR>+1> 0
         dhtmlgump 230 283 100 18 0 0 <DEF.BFONT_WHITE>NEXT PAGE
      ENDIF
   ENDFOR
ENDIF

[DIALOG d_aquarium BUTTON]
ON=1 125
REF1=<FINDCONT.<eval <ARGN>-1>>
IF (<REF1.TAG0.AquariumCreature>)
   TAG0.LiveCreatures --
ELIF (<REF1.TAG0.AquariumDecoration>)
   TAG0.Decorations --
ENDIF
REF1.BOUNCE
SDIALOG d_aquarium <eval <ARGN>-1>
RESENDTOOLTIP

[EOF]
